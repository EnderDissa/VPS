plugins {
	id 'java'
	id 'org.springframework.boot' version '3.5.6'
	id 'io.spring.dependency-management' version '1.1.6'
	id 'jacoco'
}

group = 'com.example'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

repositories {
	mavenCentral()
}

ext {
	springBootVersion = "3.5.6"
	set('springCloudVersion', "2025.0.0")
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-api:2.7.0'
	implementation 'org.springdoc:springdoc-openapi-starter-webflux-ui:2.7.0'
	implementation 'org.liquibase:liquibase-core'
	implementation 'org.springframework.cloud:spring-cloud-starter-circuitbreaker-reactor-resilience4j'

	implementation 'com.playtika.reactivefeign:feign-reactor-spring-cloud-starter:4.2.1'


	implementation 'org.mapstruct:mapstruct:1.6.3'
	compileOnly 'org.projectlombok:lombok:1.18.40'
	annotationProcessor 'org.projectlombok:lombok:1.18.40'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.6.3'

	runtimeOnly 'org.postgresql:postgresql:42.7.7'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.testcontainers:junit-jupiter:1.20.4'
	testImplementation 'org.testcontainers:postgresql:1.20.4'
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

jacoco {
	toolVersion = "0.8.12"
}

tasks.withType(Test).configureEach {
	useJUnitPlatform()
	testLogging {
		events "PASSED", "FAILED", "SKIPPED"
		exceptionFormat "full"
		showExceptions true
		showCauses true
		showStackTraces true
		showStandardStreams true
	}
	reports {
		html.required = true
		junitXml.required = true
	}
	finalizedBy tasks.jacocoTestReport
}

jacocoTestReport {
	dependsOn test
	reports {
		html.required = true
		xml.required = true
		csv.required = false
	}
}

jacocoTestCoverageVerification {
	dependsOn jacocoTestReport
	violationRules {
		rule {
			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.70
			}
		}
	}
}

check.dependsOn jacocoTestCoverageVerification
